#! /bin/csh -f
#
# Time 1-D real FFT for all supported library configurations: vDSP, FFTW, MatrixFFT. 
#
set SCRIPT_NAME=timeAll1DReal
set BASE_FILE=Real1D

source timeAllCommon

# exit doesn't work when sourcing
if($ERR_EXIT == YES) then
	exit(1)
endif

# hard coded for MFFT: always want best performance
set SKIP_TRANS=-k

#
# Set runtime limits
#
set MINSIZE=8k
if($PRECISION == Single) then
	set MAXSIZE=8m
else
	set MAXSIZE=4m
endif
if($LARGE == YES) then
	if($PRECISION == Single) then
		set MAXSIZE=64m
	else
		set MAXSIZE=32m
	endif
endif
if($HUGE == YES) then
	if($PRECISION == Single) then
		set MAXSIZE=2^30
	else
		set MAXSIZE=2^30
	endif
endif
if($QUICK == YES) then
	set MAXSIZE=1m
endif


echo ""
echo "======================= Timing 1-dimension Real FFT ======================="
echo ""

set CMD_ARGS = "$CMD_ARGS -s $MINSIZE -S $MAXSIZE"

# MatrixFFT
if($MFFT_ENABLE == YES) then
	set CMD="$LOCAL_BUILD_DIR/mtime1DReal $SKIP_TRANS $CMD_ARGS"
	echo $CMD
	echo ""
	echo $CMD >> $LOGFILE
	$CMD > $MFFT_FILE || exit(1)
    cat $MFFT_FILE
	echo ""
endif

# Accelerate
if($VDSP_ENABLE == YES) then
	set CMD="$LOCAL_BUILD_DIR/mtime1DReal -V $CMD_ARGS"
	echo $CMD
	echo ""
	echo $CMD >> $LOGFILE
	$CMD > $VSDP_FILE || exit(1)
    cat $VSDP_FILE
	echo ""
endif

# FFTW
if($FFTW_ENABLE == YES) then
	set CMD="$LOCAL_BUILD_DIR/fftwTime1D $PLAN_FLAG $CMD_ARGS"
	echo $CMD
	echo ""
	echo $CMD >> $LOGFILE
	$CMD > $FFTW_FILE || exit(1)
    cat $FFTW_FILE
	echo ""
endif

# 
# Collate the output - use all the output files we have, if some are missing, OK
# MFFT has to be first to get the correct Split/Interleaved format in the collated
# output.
#
set SOURCE_FILES=
if(-e $MFFT_FILE) then
	set SOURCE_FILES="${SOURCE_FILES} $MFFT_FILE"
endif
if(-e $VSDP_FILE) then
	set SOURCE_FILES="${SOURCE_FILES} $VSDP_FILE"
endif
if(-e $FFTW_FILE) then
	set SOURCE_FILES="${SOURCE_FILES} $FFTW_FILE"
endif

if($COLLATE_ENABLE == YES) then
	set CMD="$LOCAL_BUILD_DIR/compareFFT ${SOURCE_FILES}"
	echo $CMD
	echo ""
	echo $CMD >> $LOGFILE
	$CMD | tee $SUMM_FILE || exit(1)
endif
